#!/usr/bin/env zsh

# `man zshoptions`
# -e     ERR_EXIT
# -u     NO_UNSET
# -o pipefail | PIPE_FAIL
set -euo pipefail
IFS=$'\n\t'

cmdname=$(basename $0);

local -A nato=(
A Alfa
B Bravo
C Charlie
D Delta
E Echo
F Foxtrot
G Golf
H Hotel
I India
J Juliett
K Kilo
L Lima
M Mike
N November
O Oscar
P Papa
Q Quebec
R Romeo
S Sierra
T Tango
U Uniform
V Victor
W Whiskey
X X-ray
Y Yankee
Z Zulu
);

local -A itc=(
A Amsterdam
B Baltimore
C Casablanca
D Danemark
E Edison
F Florida
G Gallipoli
H Havana
I Italia
J Jude
K Kilogramme
L Liverpool
M Madagascar
N New\ York
O Oslo
P Paris
Q Quebec
R Roma
S Santiago
T Tripoli
U Upsala
V Valencia
W Washington
X Xanthippe
Y Yokohama
Z Zurich
);

# typeset, then set is necessary for proper array copying
typeset -A alphabet;
set -A alphabet ${(kv)nato};

fail () { echo "Error: $*" >&2; exit 1;  }

print_help() {
    # shellcheck disable=SC2086
    cat <<EOF >&2
Translates an input string into phonetic spelling
Usage:  $cmdname [options] [words to translate...]
Options:
  -h, --help             print help
  -a, --alphabet=NAME    select an alphabet
EOF
    exit 0;
}

set_alphabet () {
    case $1 in
        itc1958 ) set -A alphabet ${(kv)itc};;
        nato ) set -A alphabet ${(kv)nato};;
        * ) fail "Unknown alphabet \"$1\"";;
    esac
}

main() {
    while getopts ":a:h-:" opt; do
        case $opt in
            h)
                print_help
                exit
                ;;
            a)
                set_alphabet "${OPTARG%%=*}";
                ;;
            -) case $OPTARG in
                alphabet=* ) set_alphabet ${OPTARG#*=};;
                help ) print_help;;
                * ) fail "Unknown long option \"${OPTARG%%=*}\"";;
                esac;;
            : ) fail "Short option \"$OPTARG\" missing argument";;
            \?)
                fail "Invalid option"
                exit
                ;;
        esac
    done

    shift $((OPTIND-1))

    input="$@";
    input_len="${#input}";

    # echo "$alphabet";

    for ((i=1; i<$input_len+1; i++)) do
        char="${input[$i]:u}"
        # echo "'${char}'";
        if [[ "$char" =~ ^[A-Z]$ ]]; then
            echo "${alphabet[$char]}"
        elif [[ "$char" =~ [[:space:]]+ ]]; then
            echo;
        else
            echo "$char";
        fi
    done
}

main "${@:-}"
