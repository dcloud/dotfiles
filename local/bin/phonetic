#!/usr/bin/env zsh

# `man zshoptions`
# -e     ERR_EXIT
# -u     NO_UNSET
# -o pipefail | PIPE_FAIL
set -euo pipefail
IFS=$'\n\t'

cmdname=$(basename $0);

typeset -A nato=(
A Alfa
B Bravo
C Charlie
D Delta
E Echo
F Foxtrot
G Golf
H Hotel
I India
J Juliett
K Kilo
L Lima
M Mike
N November
O Oscar
P Papa
Q Quebec
R Romeo
S Sierra
T Tango
U Uniform
V Victor
W Whiskey
X X-ray
Y Yankee
Z Zulu
);

fail () { echo "Error: $*" >&2; exit 1;  }

print_help() {
    # shellcheck disable=SC2086
    cat <<EOF >&2
Translates an input string into phonetic spelling
Usage:  $cmdname [options] [words to translate...]
Options:
  -h, --help    print help
EOF
    exit 0;
}

main() {
    while getopts "h-:" opt; do
        case $opt in
            h)
                print_help
                exit
                ;;
            -) case $OPTARG in
                help ) print_help;;
                * ) fail "Unknown long option \"${OPTARG%%=*}\"";;
                esac;;
            \?)
                fail "Invalid option"
                exit
                ;;
        esac
    done

    input="$@";
    input_len="${#input}";

    for ((i=1; i<$input_len+1; i++)) do
        char="${input[$i]:u}"
        # echo "'${char}'";
        if [[ "$char" =~ ^[A-Z]$ ]]; then
            echo "${nato[$char]}"
        elif [[ "$char" =~ [[:space:]]+ ]]; then
            echo;
        else
            echo "$char";
        fi
    done
}

main "${@:-}"
